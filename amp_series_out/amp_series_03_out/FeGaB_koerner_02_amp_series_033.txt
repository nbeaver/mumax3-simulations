
f 		:= 1425000000.0												// excitation freq				[Hz]
bstat 	:= 0.0012												// static field					[T]
amp 	:= 0.0001048113												// excitation amplitude 		[T_p]

N := 256														// number of cells
c := 20e-9														// cell width					[m]
d := 20e-9														// cell height					[m]
setgridsize(N, N, 1)	
setcellsize(c, c, d)
//setpbc(50,50,0)

// average FeGaB magnetic parameters set for region 0
alpha 	= 0.02
Ku1   	= 1035													// uniaxial anisotropy			[J/m^3]
Aex   	= 1.49e-11												// exchange energy				[J/m]		http://www.cwscholz.net/projects/diss/html/node58.html
Msat  	= 1030000.0													// saturation magnetization 	[A/m]

// define regions
defregion(1, xrange(0, inf))  // left half
defregion(2, xrange(-inf, 0)) // right half

// set parameters for region 1
Ku1.setregion(1, Ku1.GetRegion(0))
anisU.setRegion(1, vector(1, 0, 0))
Msat.SetRegion(1, Msat.GetRegion(0)*0.8)
alpha.SetRegion(1, alpha.GetRegion(0))	
Aex.SetRegion(1, Aex.GetRegion(0))	

// set parameters for region 2
Ku1.setregion(2, Ku1.GetRegion(0))
anisU.setRegion(2, vector(1, 0, 0))
Msat.SetRegion(2, Msat.GetRegion(0))
alpha.SetRegion(2, alpha.GetRegion(0))	
Aex.SetRegion(2, Aex.GetRegion(0))	
			
// save starting conditions as .ovf
save(regions)
save(Ku1)
save(AnisU)
save(Msat)
save(exchCoupling)


// save starting conditions as .png
Snapshotas(regions,"regions_init.png")
Snapshotas(AnisU,"AnisU_init.png")
Snapshotas(alpha,"alpha_init.png")
Snapshotas(Ku1,"Ku1_init.png")
Snapshotas(Msat,"Msat_init.png")


// relax to static field
// static field in x direction
B_ext = vector(bstat, 0, 0)			

m = uniform(1,0,0)		//.Add(0.1, randomMag());

// remove surface charges left/right
BoundaryRegion := 0
MagLeft        := 1
MagRight       := 1
ext_rmSurfaceCharge(BoundaryRegion, MagLeft, MagRight)

// relax M to x direction											
run(10e-9)		

saveas(m,"m_relax.ovf")
Snapshotas(m,"m_relax.png")

// static + excitation field
B_ext = vector(bstat, amp*sin(2*pi*f*t), 0)
// settling time
run(200e-9)														

//Simulation Time
points 		:= 8192								
tstep 		:= 12.5e-12											// 10ns -> largest fft-freq = 5GHz					12.5e-12s -> 40GHz
simtime 	:= tstep * points

//TableAdd(Crop(B_ext, 64, 65, 64, 65, 0, 1))					// cols 11,12,13 = B_ext
//TableAutoSave(tstep)

//save m_full as .ovf
autosave(m_full, tstep)	

// limit max solver step to avoid missing points in fft
maxdt = 1e-12												

// run simulation
run(simtime)
